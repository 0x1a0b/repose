apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    fatJar
}

dependencies {
    compile project(":repose-aggregator:tests:test-containers:test-container-api")
    compile "org.glassfish.main.extras:glassfish-embedded-all"
    compile "org.slf4j:slf4j-api"
    compile "org.apache.logging.log4j:log4j-slf4j-impl"
    compile "org.apache.logging.log4j:log4j-api"
    compile "org.apache.logging.log4j:log4j-core"
    compile "commons-cli:commons-cli"
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openrepose.commons.utils.test.glassfish.ReposeGlassfishServer'
    }
}

shadowJar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

artifacts {
    fatJar shadowJar
}

jacocoTestCoverageVerification {
    violationRules {
        rules.forEach { rule ->
            rule.limits.forEach { limit ->
                if (limit.counter == 'INSTRUCTION' && limit.value == 'COVEREDRATIO') {
                    // todo: set to the current coverage -- need more coverage, but we don't want the build to fail for the baseline
                    limit.minimum = 0.00
                }
            }
        }
    }
}

build.dependsOn shadowJar

apply plugin: 'ear'
apply plugin: 'nebula.ospackage'

dependencies {
    earlib project(":repose-aggregator:components:filters:api-validator-filter")
    earlib project(":repose-aggregator:components:filters:simple-rbac-filter")
}

publishing {
    publications {
        nebula(MavenPublication) {
            artifact source: ear, extension: 'ear'
            artifact source: buildDeb, extension: 'deb'
            artifact source: buildRpm, extension: 'rpm'

            pom.withXml {
                asNode().appendNode('packaging', 'ear')
            }
        }
    }
}

ospackage {
    packageName = "repose-extensions-filter-bundle"
    release = 1
    os = LINUX
    packageDescription 'Package for the Repose Extensions Filter Bundle'
    priority 'optional'
    packageGroup 'Applications/Internet'
    maintainer 'Repose Development <repose-development@lists.openrepose.org>'
    url 'http://openrepose.org'

    //todo: write and submit a pr to the plugin that does this automatically
    if(project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        signingKeyId project.getProperty('signing.keyId')
        signingKeyPassphrase project.getProperty('signing.password')
        signingKeyRingFile file(project.getProperty('signing.secretKeyRingFile'))
    }

    into '/usr/share/repose/filters'

    //the bundle
    from(ear.outputs.files) {
        user 'root'
        permissionGroup 'root'
        fileMode 555
    }

    //todo: remove the old files once the gradle build is the main
    //the configs
    from('src/config/filters') {
        into '/etc/repose'
        user 'repose'
        permissionGroup 'repose'
        fileMode 640
        fileType CONFIG | NOREPLACE
    }

    //todo: remove the old Dependencies file after the move from maven
    //the license info
    from(["$rootDir/LICENSE.txt", "$rootDir/CONTRIBUTORS.txt", 'src/config/docs/DEPENDENCIES.txt']) {
        into '/usr/share/doc/repose-extensions-filter-bundle'
        user 'root'
        permissionGroup 'root'
        fileMode 444
    }

    requires('repose', project.version, EQUAL)

    replaces('repose-extension-filters')
}

buildDeb {
    requires('java8-runtime-headless')
}

buildRpm {
    requires('java', '1.8.0', GREATER | EQUAL)
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import groovy.json.JsonSlurper

apply plugin: 'com.bmuschko.docker-remote-api'

// This forces the usage of a more recent unix-socket-factory which makes Mac OS X happy
// It also forces the usage of jackson dependencies which were not being brought in as expected
// TODO: Remove unix-socket-factory after upgrading to gradle-docker-plugin 3.0.4
configurations {
    dockerJava {
        resolutionStrategy {
            force 'de.gesellix:unix-socket-factory:2016-04-06T22-21-19'
            force 'com.fasterxml.jackson.core:jackson-databind:2.6.4'
            force 'com.fasterxml.jackson.core:jackson-core:2.6.4'
            force 'com.fasterxml.jackson.core:jackson-annotations:2.6.4'
        }
    }
}

String reposeVersion = project.hasProperty('repose-version') ? project.property('repose-version') as String : version as String

docker {
    url = "unix:///var/run/docker.sock"

    registryCredentials {
        username = project.hasProperty('dockerhub.username') ? project.property('dockerhub.username') : 'Need to provide a Docker Hub username'
        password = project.hasProperty('dockerhub.password') ? project.property('dockerhub.password') : 'Need to provide a Docker Hub password'
        email = 'reposecore@rackspace.com'
    }
}

['Ubuntu': '', 'Centos': '-centos'].forEach { baseImage, tagSuffix ->
    task("build${baseImage}Image", type: DockerBuildImage) {
        finalizedBy "remove${baseImage}Image"
        inputDir = file("$projectDir/src/docker/${baseImage.toLowerCase()}")
        buildArgs = ['REPOSE_VERSION': reposeVersion]
    }

    task("tag${baseImage}ImageVersion", type: DockerTagImage) {
        dependsOn "build${baseImage}Image"
        repository = 'rackerlabs/repose'
        tag = reposeVersion + tagSuffix
        targetImageId { tasks["build${baseImage}Image"].getImageId() }
    }

    task("tag${baseImage}ImageLatest", type: DockerTagImage) {
        dependsOn "build${baseImage}Image"
        repository = 'rackerlabs/repose'
        tag = "latest${tagSuffix}"
        targetImageId { tasks["build${baseImage}Image"].getImageId() }
    }

    task("push${baseImage}ImageVersion", type: DockerPushImage) {
        dependsOn "tag${baseImage}ImageVersion"
        imageName = 'rackerlabs/repose'
        tag = reposeVersion + tagSuffix
    }

    task("push${baseImage}ImageLatest", type: DockerPushImage) {
        dependsOn "tag${baseImage}ImageLatest"
        imageName = 'rackerlabs/repose'
        tag = "latest${tagSuffix}"
    }

    task("push${baseImage}Image") {
        group = 'docker'
        dependsOn "push${baseImage}ImageVersion", "push${baseImage}ImageLatest"
    }

    task("remove${baseImage}Image", type: DockerRemoveImage) {
        dependsOn "build${baseImage}Image"
        mustRunAfter "tag${baseImage}ImageVersion", "tag${baseImage}ImageLatest", "push${baseImage}ImageVersion", "push${baseImage}ImageLatest"
        targetImageId { tasks["build${baseImage}Image"].getImageId() }
        force = true
    }

    tasks["push${baseImage}ImageLatest"].onlyIf { isLatestDockerHubVersion(reposeVersion) }
}

boolean isLatestDockerHubVersion(String version) {
    boolean isLatest = true

    String nextPage = 'https://hub.docker.com/v2/repositories/rackerlabs/repose/tags/'
    while (nextPage && isLatest) {
        Map jsonObject = new JsonSlurper().parse(new URL(nextPage)) as Map
        List<String> otherVersions = jsonObject.results.collect { it.name }

        if (!isLatestVersion(version, otherVersions)) {
            isLatest = false
        }

        nextPage = jsonObject.next
    }

    return isLatest
}

import org.ajoberstar.grgit.Grgit
import java.text.SimpleDateFormat

apply plugin: 'org.asciidoctor.convert'

dependencies {
    asciidoctor 'org.asciidoctor:asciidoctorj-diagram'
}

task copyXsds(type: Copy) {
    // Copy the Repose Core configuration schemas.
    from("${project.projectDir}/core/repose-core-api/src/main/resources/META-INF/schema/container/container-configuration.xsd")
    from("${project.projectDir}/core/repose-core-api/src/main/resources/META-INF/schema/metrics/metrics.xsd")
    from("${project.projectDir}/core/repose-core-api/src/main/resources/META-INF/schema/system-model/system-model.xsd")

    // Copy all filter and service configuration schemas.
    from(fileTree("${project.projectDir}/components").include('**/src/main/resources/META-INF/schema/config/*.xsd').files)

    // Place all schemas into a schemas directory.
    into("${project.buildDir}/asciidoc/html5/schemas/")
}

asciidoctor {
    requires 'asciidoctor-diagram'
    options template_dirs: ["${project.projectDir}/src/docs/asciidoc/_backends/haml"]
    attributes 'source-highlighter': 'coderay',
               'stylesdir': "${project.projectDir}/src/docs/asciidoc/_stylesheets",
               'stylesheet': 'repose.css',
               'toc': 'left',
               'icons': 'font',
               'docinfo': 'shared'

    resources {
        from 'src/docs/resources'

        into '.'
    }
}

task('publishDocs', dependsOn: asciidoctor, group: 'release', description: 'Adds the new version of the docs to github pages') << {
    def workingDir = project.file("${project.buildDir}/gh-pages")

    workingDir.deleteDir()

    def repo = Grgit.clone(
            uri: 'git@github.com:rackerlabs/repose.git',
            refToCheckout: 'gh-pages',
            dir: workingDir,
    )

    //add to versions list
    def versionsFile = project.file("${project.buildDir}/gh-pages/_includes/versions.html")
    def latestLinkFile = project.file("${project.buildDir}/gh-pages/versions/latest")
    def versionLines = versionsFile.readLines()
    def date = new SimpleDateFormat('yyyy-MM-dd').format(new Date())
    def xmlSlurper = new XmlSlurper()
    def firstVersionIndex = 1
    def lastVersionIndex = versionLines.size() - 1
    def newVersionIndex = lastVersionIndex
    // Always skip the first and last lines, which are the latest version line and old wiki line
    for (int i = firstVersionIndex; i < lastVersionIndex; ++i) {
        String existingVersion = (xmlSlurper.parseText(versionLines[i]).a.@href =~ /\.\/versions\/(\d\.\d\.\d\.\d)\/index.html/)[0][1]
        if (compareVersions(String.valueOf(version), existingVersion) > 0) {
            newVersionIndex = i
            break
        }
    }
    if (newVersionIndex == firstVersionIndex) {
        ['ln', '-s', '-f', version as String, latestLinkFile.absolutePath].execute().waitFor()
    }
    versionLines.add(newVersionIndex, """<li><a href="./versions/$version/index.html">v$version - $date</a></li>""")
    def writer = new PrintWriter(versionsFile.newWriter())
    versionLines.each {
        writer.println(it)
    }
    writer.close()

    copy {
        from "${asciidoctor.outputDir}/html5"
        into "${project.buildDir}/gh-pages/versions/$version"
    }

    repo.with {
        add(patterns: ['.'])
        commit(message: "Adding documentation for $version")
        push()
    }
}

project.tasks.getByPath(':release').dependsOn publishDocs

asciidoctor.dependsOn copyXsds
build.dependsOn asciidoctor

/**
 * @return -1 if v1 is a lower version than v2, 0 if they are equal, and 1 if v1 is a greater version than v2
 */
int compareVersions(String v1, String v2) {
    List<String> v1Parts = v1.tokenize('.')
    List<String> v2Parts = v2.tokenize('.')

    int minLen = Math.min(v1Parts.size(), v2Parts.size())

    // Compare each number from the versions until either the numbers do not match,
    // or the end of a version string is hit.
    for (int i = 0; i < minLen; ++i) {
        int n1 = v1Parts[i] as int
        int n2 = v2Parts[i] as int

        if (n1 != n2) {
            return n1 <=> n2
        }
    }

    // If we got this far, then one version is a substring of the other.
    // Therefore, whichever version is longer is the greater version.
    return v1Parts.size() <=> v2Parts.size()
}

boolean isLatestVersion(String version, List<String> otherVersions) {
    return !otherVersions.any {
        it.length() > 0 && it.charAt(0).isDigit() && compareVersions(version, it) < 0
    }
}

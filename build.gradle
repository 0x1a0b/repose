buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    }
}

//This is needed to use the Replace tokens ant task in any projects.
// It cannot be imported in the project, it must be imported at the parent level
import org.apache.tools.ant.filters.ReplaceTokens


allprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'groovy'

//We want to have all these plugins configured for us
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'

//All our projects will use mavenCentral for their maven source (at least)
    repositories {
        mavenCentral()
        maven {
            //TODO: maybe need creds: http://gradle.org/docs/current/userguide/dependency_management.html#sub:maven_repo
            url "https://maven.research.rackspacecloud.com/content/groups/aggregate"
        }
        maven {
            //For play-json
            url "http://repo.typesafe.com/typesafe/maven-releases"
        }
    }
    /*
 * If you want to override dependencies, you can do it here. You can also ban them from here I think
 */
    configurations {
        all {
            resolutionStrategy.eachDependency {
                //Make sure that all commons-loggings are replaced by the jcl-over-slf4j bridge
                if (it.requested.name == 'commons-logging') {
                    it.useTarget 'org.slf4j:jcl-over-slf4j:1.7.7'
                }
            }
        }
        //Needed to set up the scala compiler by hand
        scalaCompiler
        //I need a jaxb configuration classpath
        jaxbTasks
    }
/*
 * Set our source levels
 */
    compileJava {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }

//Tell the scala compile tasks not to use the ant based compiler
    tasks.withType(ScalaCompile) {
        scalaCompileOptions.useAnt = false
    }

    /**
     * This pile of stuff is required to have scalac be our joint java/scala compiler and avoid having to specify
     * it for each project. The alternative is to specify the sourceSets logic on projects where java code depends
     * on scala code (in the same project). There's no other way to alter the ordering of the compile steps, java
     * always gets compiled first.
     */
    //Tell this project to do joint compilation
    sourceSets.main.scala.srcDir "src/main/java"
    sourceSets.main.java.srcDirs = []

    //This is separate from the rest of the dependencies to make it obvious why it's here
    dependencies {
        scalaCompiler "org.scala-lang:scala-compiler:${scalaVersion}"

    }

    tasks.withType(ScalaCompile) {
        scalaClasspath = configurations.scalaCompiler
    }
    /**
     * END OF MANUAL SCALA COMPILER SETTING
     */

/**
 * Create all the source directories, a handy task
 */
    task 'create-dirs' << {
        sourceSets.all { set ->
            set.allSource.srcDirs.each { it.mkdirs() }
        }
    }

    test {
        //Set this for stupid xml and it's anger at log4j2
        systemProperty "javax.xml.parsers.DocumentBuilderFactory", "com.sun.org.apache.xerces.internal.jaxp.DocumentBuilderFactoryImpl"

        filter {
            includeTestsMatching "*Test"
        }
    }

    dependencies {
        testCompile "org.codehaus.groovy:groovy-all:2.1.3",
                "org.spockframework:spock-core:0.7-groovy-2.0",
                "org.hamcrest:hamcrest-all:1.3",                //This order matters!
                "org.powermock:powermock-module-junit4:1.5.4",  //This order matters!
                "org.powermock:powermock-api-mockito:1.5.4",    //This order matters!
                "org.mockito:mockito-all:1.9.5",                //This order matters!
                "junit:junit:4.11",                          //This order matters!
                "org.glassfish:javax.servlet:3.1", //TODO: This shouldn't be here
                "javax:javaee-web-api:6.0", //TODO: this shouldn't be here
                "javax.transaction:transaction-api:1.1", //TODO: this shouldn't be here

                "org.apache.logging.log4j:log4j-api:${log4jVersion}",
                "org.apache.logging.log4j:log4j-core:${log4jVersion}",
                "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
        testCompile("com.mockrunner:mockrunner-servlet:1.0.0") {
            exclude module: 'commons-logging'
            exclude module: 'servlet-api'
            exclude module: 'xercesImpl'
        }


        testCompile group: "org.apache.logging.log4j", name: "log4j-core", version: "${log4jVersion}", classifier: "tests"

        //Use the latest version of the zinc compiler
        zinc "com.typesafe.zinc:zinc:0.3.7"

        //Jaxb compiler stuffs
        jaxbTasks 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41',
                'com.sun.xml.bind:jaxb-impl:2.2.7-b41',
                'javax.xml.bind:jaxb-api:2.2.7'


    }

    /**
     * Jaxb translation tasks that needs to happen before the jaxb stuff can actually be run
     * This removes the XSD 1.1 elements that jaxb does not support
     */

    def jaxbSafeXSDSources = "${projectDir}/build/src/generated-xsd"
    def generatedSources = "${projectDir}/build/src/generated-sources"

    //Don't forget to add that generated sources to the list of things we need to do
    //ADD IT TO THE SCALA SOURCES
    sourceSets.main.scala.srcDirs += generatedSources

    task transformXSDs() {
        description "Makes our XSDs safe for jaxb, because XSD 1.1, or just copy them if there's no XSD to translate"

        def targetDir = file(jaxbSafeXSDSources)

        def schemaBaseDir = "$projectDir/src/main/resources/META-INF/schema"
        def xsltFile = "${rootDir}/gradle/xsl/remove-1.1-elements.xsl"

        //If they set a property for the jaxbXSLT file override our default
        if (project.hasProperty('jaxbXsltFile')) {
            xsltFile = project.jaxbXsltFile
        }

        if (project.hasProperty('jaxbSchemaBase')) {
            schemaBaseDir = project.jaxbSchemaBase
        }

        inputs.files(
                fileTree(dir: schemaBaseDir, includes: ["**/*.xsd", "**/*.xjb"]),
                file(xsltFile)
        )
        outputs.dir jaxbSafeXSDSources

        doLast {
            targetDir.mkdirs()

            //Find all XSDs in the src/main/resources/META-INF.schema
            if (file(xsltFile).exists()) {
                logger.lifecycle("Transforming Schemas")
                ant.xslt(
                        extension: '.xsd',
                        basedir: schemaBaseDir,
                        includes: "**/*.xsd",
                        style: xsltFile,
                        destdir: "${jaxbSafeXSDSources}"
                )
            } else {
                logger.lifecycle("NOT Transforming Schemas")
                ant.copy(todir: jaxbSafeXSDSources) {
                    fileset(dir: schemaBaseDir, includes: "**/*.xsd")

                }

            }

            //Copy over any bindings.xjb into their appropriate directories
            ant.copy(
                    todir: jaxbSafeXSDSources
            ) {
                fileset(dir: schemaBaseDir, includes: "**/*.xjb")
            }
        }
    }

    /**
     * Not using the jaxb plugin because it cannot support multiple directories of .binding files:
     * https://github.com/jacobono/gradle-jaxb-plugin#jaxb-plugin-convention
     * it cannot handle subfolders, so we'd have to specify the task each time for each folder
     */
    task jaxb(dependsOn: 'transformXSDs') {
        description "compiles XSDs to classes"
        def jaxbTargetDir = file(generatedSources)
        inputs.source(jaxbSafeXSDSources)
        outputs.dir generatedSources

        doLast {
            jaxbTargetDir.mkdirs()
            ant.taskdef(name: 'xjc',
                    classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.jaxbTasks.asPath
            )
            ant.jaxbTargetDir = jaxbTargetDir
            logger.lifecycle("Generating the JaxBees")
            ant.xjc(
                    destdir: '${jaxbTargetDir}',
                    extension: true //Some of our jaxb stuff requires extensions...
            ) {
                schema(dir: jaxbSafeXSDSources, includes: "**/*.xsd")
                binding(dir: jaxbSafeXSDSources, includes: "**/*.xjb")
            }
        }
    }
}

/**
 * Commons projects
 */

project(":repose-aggregator:commons:configuration") {
    dependencies {
        compile project(":repose-aggregator:commons:utilities"),
                project(":repose-aggregator:core:core-service-api"),
                "xerces:xerces-xsd11:2.12.0-rax",
                "xalan:xalan:2.7.1",
                "commons-pool:commons-pool:${commonsPoolVersion}"

        testCompile "org.scala-lang:scala-library:${scalaVersion}",
                "org.scalatest:scalatest_2.10:$scalaTestVersion",
                "com.typesafe.scala-logging:scala-logging-slf4j_2.10:${scalaLoggingVersion}"
    }
}

project(":repose-aggregator:commons:utilities") {
    dependencies {
        compile project(":repose-aggregator:services:httpclient:api"),
                "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxbRuntimeVersion}",
                "org.apache.httpcomponents:httpclient:$httpClientVersion",
                "commons-pool:commons-pool:$commonsPoolVersion",
                "commons-codec:commons-codec:1.10",
                "org.apache.commons:commons-lang3:$commonsLangVersion",
                "com.typesafe.scala-logging:scala-logging-slf4j_2.10:$scalaLoggingVersion",
                "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
                "javax.ws.rs:javax.ws.rs-api:2.0.1",
                "org.apache.logging.log4j:log4j-api:$log4jVersion",
                "org.glassfish:javax.servlet:3.1", //TODO: this API is some weird hybrid of 2.5 and 3.1.0
                "org.slf4j:slf4j-api:$slf4jVersion",
                "org.apache.httpcomponents:httpcore:$httpClientVersion"

        testCompile "org.scalatest:scalatest_2.10:$scalaTestVersion"

    }
}
/**
 * END OF COMMONS
 */

project(":repose-aggregator:core:core-service-api") {

    //Add this bit if you want JAXB processing to happen first
    //The tasks are customized for the way we store files.
    tasks.compileScala.dependsOn(jaxb)

    dependencies {
        compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxbRuntimeVersion}",
                project(":repose-aggregator:commons:utilities"),
                "com.google.guava:guava:$guavaVersion"
    }
}

//We got through the entire :repose-aggregator:commons:configuration without having to configure anything for it :|
project(":repose-aggregator:components:cli-utils") {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    mainClassName = "org.openrepose.cli.CommandDriver"

    shadowJar {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    publishing {
        publications {
            shadow(MavenPublication) {
                from components.java
                artifact shadowJar
            }
        }
    }

    //TODO: NEEDS THE SHADE PLUGIN
    dependencies {
        compile project(":repose-aggregator:core:core-lib"),
                project(":repose-aggregator:services:datastore:api"),
                project(":repose-aggregator:commons:utilities")
    }

    artifacts {
        shadow shadowJar
    }

    tasks.build.dependsOn("shadowJar")
}

project(":repose-aggregator:core:core-lib") {
    configurations {
        testingArtifacts {
            transitive = false
        }
    }

    dependencies {
        compile "javax.inject:javax.inject:1",
                project(":repose-aggregator:external:jee6-schemas"),
                project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:commons:utilities"),
                project(":repose-aggregator:commons:configuration"),
                project(":repose-aggregator:services:service-client:api"),
                project(":repose-aggregator:services:service-client:impl"),
                project(":repose-aggregator:services:health-check:api"),
                project(":repose-aggregator:services:health-check:impl"),
                project(":repose-aggregator:services:httpclient:api"),
                project(":repose-aggregator:services:httpclient:impl"),
                project(":repose-aggregator:services:datastore:api"),
                project(":repose-aggregator:services:datastore:impl"),
                project(":repose-aggregator:services:phone-home"),
                "org.apache.httpcomponents:httpclient:$httpClientVersion",
                "org.springframework:spring-core:$springVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-web:$springVersion",
                "com.google.guava:guava:$guavaVersion",
                "commons-io:commons-io:2.4",
                "org.apache.commons:commons-lang3:$commonsLangVersion",
                "joda-time:joda-time:2.6",
                "org.eclipse.jetty:jetty-server:$jettyVersion",
                "com.yammer.metrics:metrics-core:$yammerVersion",
                "com.yammer.metrics:metrics-graphite:$yammerVersion",
                "javax.ws.rs:javax.ws.rs-api:$javaxWsApiVersion",
                "org.apache.logging.log4j:log4j-flume-ng:$log4jVersion",
                "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
                "org.apache.logging.log4j:log4j-api:$log4jVersion",
                "org.apache.logging.log4j:log4j-core:$log4jVersion",
                "org.apache.logging.log4j:log4j-jul:$log4jVersion",
                "org.apache.logging.log4j:log4j-iostreams:$log4jVersion",
                "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
                "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
                "com.typesafe:config:$typesafeConfigVersion",
                "org.scala-lang:scala-library:$scalaVersion",
                "javax.servlet:javax.servlet-api:3.1.0"

        testCompile "org.springframework:spring-test:$springVersion",
                "org.scalatest:scalatest_2.10:$scalaTestVersion",
                "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        testingArtifacts "org.openrepose.filters.core.test:core-test-filter-bundle:$testArtifactVersion",
                "org.openrepose.filters.core.test:busted-application-name-ear:$testArtifactVersion",
                "org.openrepose.filters.core.test:busted-web-fragment-ear:$testArtifactVersion",
                "org.openrepose.filters.core.test:second-filter-bundle:$testArtifactVersion"
    }

    task copyTestArtifacts << {
        copy {
            from configurations.testingArtifacts
            into 'build/earFiles' //TODO: this is going to require additional changes to the source tree

        }
    }

//For the ReplaceTokens ant filter

/*
TODO: GOING TO HAVE TO DUPE RESOURCES OR SOMETHING, CANT EXIST IN PARALLEL WITH MAVEN :(
This requires all filtering to be done by @value@, not the maven style of doing ${some.value}
 */
    processResources {
        //Hack all the properties to a toString value, because I don't care!
        filter(ReplaceTokens, tokens: project.properties.collectEntries { k, v -> [k.toString(), v.toString() ?: ''] })
    }

    processTestResources {
        //Hack all the properties to a toString value, because I don't care!
        filter(ReplaceTokens, tokens: project.properties.collectEntries { k, v -> [k.toString(), v.toString() ?: ''] })
    }

//Make sure we have our test artifacts before testing
    test.dependsOn("copyTestArtifacts")

    //We can joint compile the java and scala, and are already doing that,
    //and so we need to ensure that the scala stuff is compiled first for this project,
    // Some of the groovy code references scala code.
    compileTestGroovy.dependsOn("compileTestScala")
}

project(":repose-aggregator:core:valve") {
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            shadow(MavenPublication) {
                from components.java
                artifact shadowJar
            }
        }
    }

    dependencies {
        compile project(":repose-aggregator:core:core-lib"),
                project(":repose-aggregator:core:core-service-api"),
                "org.scala-lang:scala-library:$scalaVersion",
                "com.github.scopt:scopt_2.10:3.2.0",
                "com.typesafe:config:$typesafeConfigVersion",
                "org.springframework:spring-web:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.springframework:spring-core:$springVersion",
                "org.eclipse.jetty:jetty-server:$jettyVersion",
                "org.eclipse.jetty:jetty-servlet:$jettyVersion",
                "org.eclipse.jetty:jetty-util:$jettyVersion",
                "org.eclipse.jetty:jetty-webapp:$jettyVersion",
                "com.typesafe.scala-logging:scala-logging-slf4j_2.10:$scalaLoggingVersion",
                "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
                "org.apache.logging.log4j:log4j-core:$log4jVersion"
        //TODO: do I need to include httpclient here because it's needed in tests now, or will shadow jar be smarter than maven's shade?

        testCompile "javax.servlet:javax.servlet-api:3.1.0",
                "org.scalatest:scalatest_2.10:$scalaTestVersion",
                "org.apache.httpcomponents:httpclient:$httpClientVersion"
    }

    processResources {
        //Hack all the properties to a toString value, because I don't care!
        filter(ReplaceTokens, tokens: project.properties.collectEntries { k, v -> [k.toString(), v.toString() ?: ''] })
    }

    processTestResources {
        from('src/main/resources') {
            //Copy all the resources without filtering!
            //This is needed because filtering breaks the binary .jks file, but we still need to copy it
        }
        from('src/main/resources') {
            //Copy the resources that actually need to be filtered!
            include '**/*.conf'
            //Hack all the properties to a toString value, because I don't care!
            filter(ReplaceTokens, tokens: project.properties.collectEntries { k, v -> [k.toString(), v.toString() ?: ''] })
        }
    }

    mainClassName = "org.openrepose.valve.Main"

    shadowJar {
        //TODO: have to deal with the log4jPlugins.dat from the log4j-flume-ng dependency ?
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    tasks.build.dependsOn("shadowJar")

    artifacts {
        shadow shadowJar
    }
}

project(":repose-aggregator:core:web-application") {
    apply plugin: 'war'

    configurations {
        warFile
    }

    artifacts {
        warFile war
    }

    war.archiveName = "ROOT.war"

    dependencies {
        compile project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:core:core-lib"),
                "com.typesafe:config:$typesafeConfigVersion",
                "org.scala-lang:scala-library:$scalaVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-web:$springVersion",
                "org.springframework:spring-core:$springVersion",
                "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
                "org.apache.logging.log4j:log4j-core:$log4jVersion"

        testCompile "org.scalatest:scalatest_2.10:$scalaTestVersion"
    }
}

/**
 * SERVICES
 */

project(":repose-aggregator:services:phone-home") {
    dependencies {
        compile project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:commons:utilities"),
                project(":repose-aggregator:services:service-client:api"),
                "org.scala-lang:scala-library:$scalaVersion",
                "com.typesafe.scala-logging:scala-logging-slf4j_2.10:$scalaLoggingVersion",
                "com.typesafe.play:play-json_2.10:$playJsonVersion",
                "org.springframework:spring-beans:$springVersion",
                "javax.inject:javax.inject:1"

        testCompile "org.scalatest:scalatest_2.10:$scalaTestVersion",
                "com.sun.jersey:jersey-server:$jerseyServerVersion"
    }
}

project(":repose-aggregator:services:service-client:api") {
    dependencies {
        compile project(":repose-aggregator:commons:utilities")
    }
}

project(":repose-aggregator:services:service-client:impl") {
    dependencies {
        compile project(":repose-aggregator:commons:utilities"),
                project(":repose-aggregator:services:service-client:api"),
                project(":repose-aggregator:services:httpclient:api"),
                "com.typesafe.akka:akka-actor_2.10:2.2.3",
                "com.typesafe:config:$typesafeConfigVersion",
                "com.google.guava:guava:$guavaVersion",
                "commons-io:commons-io:2.4",
                "org.scala-lang:scala-library:$scalaVersion",
                "javax.inject:javax.inject:1"

        testCompile "org.scalatest:scalatest_2.10:$scalaTestVersion",
                "com.sun.jersey:jersey-server:$jerseyServerVersion",
                "com.typesafe.scala-logging:scala-logging-slf4j_2.10:$scalaLoggingVersion",
                "org.eclipse.jetty:jetty-server:$jettyVersion"

        //TODO: the mavens did some extraction of the reference.conf for akka, but I really think that's not necessary....
    }
}

project(":repose-aggregator:services:datastore:api") {
    dependencies {
        compile project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:commons:utilities")
    }
}

project(":repose-aggregator:services:datastore:impl") {

    tasks.compileScala.dependsOn(jaxb)

    dependencies {
        compile project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:services:datastore:api"),
                project(":repose-aggregator:services:health-check:api"),
                project(":repose-aggregator:commons:utilities"),
                project(":repose-aggregator:services:rate-limiting-service"),
                "com.google.guava:guava:$guavaVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$jaxbRuntimeVersion",
                "org.eclipse.jetty:jetty-server:$jettyVersion",
                "org.eclipse.jetty:jetty-servlet:$jettyVersion",
                "net.sf.ehcache:ehcache-core:2.6.0",
                "com.yammer.metrics:metrics-ehcache:$yammerVersion",
                "org.apache.commons:commons-lang3:$commonsLangVersion",
                "javax.inject:javax.inject:1"

        testCompile "xerces:xerces-xsd11:$xercesRaxVersion"
    }
}

project(":repose-aggregator:services:rate-limiting-service") {

    tasks.compileScala.dependsOn(jaxb)

    dependencies {
        compile project(":repose-aggregator:services:datastore:api"),
                "xerces:xerces-xsd11:$xercesRaxVersion",
                "org.apache.commons:commons-lang3:$commonsLangVersion",
                "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$jaxbRuntimeVersion"

    }
}
project(":repose-aggregator:services:health-check:api") {
    dependencies {
        compile project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:commons:utilities")

    }
}

project(":repose-aggregator:services:health-check:impl") {
    dependencies {
        compile project(":repose-aggregator:services:health-check:api"),
                "javax.inject:javax.inject:1"
    }
}
project(":repose-aggregator:services:httpclient:api") {
    dependencies {
        compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    }
}

project(":repose-aggregator:services:httpclient:impl") {

    tasks.compileScala.dependsOn(jaxb)

    dependencies {
        compile project(":repose-aggregator:services:httpclient:api"),
                project(":repose-aggregator:core:core-service-api"),
                project(":repose-aggregator:services:health-check:api"),
                "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$jaxbRuntimeVersion",
                "org.apache.httpcomponents:httpclient:4.2.5", //TODO: commons-codec exclusion?
                "javax.inject:javax.inject:1"

        testCompile "xerces:xerces-xsd11:$xercesRaxVersion"

    }
}

/**
 * END OF SERVICES
 */

/**
 * EXTERNAL
 */

project(":repose-aggregator:external:jee6-schemas") {

    tasks.compileScala.dependsOn(jaxb)

    dependencies {
        compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$jaxbRuntimeVersion"
    }
}